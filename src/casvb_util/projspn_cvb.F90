!***********************************************************************
! This file is part of OpenMolcas.                                     *
!                                                                      *
! OpenMolcas is free software; you can redistribute it and/or modify   *
! it under the terms of the GNU Lesser General Public License, v. 2.1. *
! OpenMolcas is distributed in the hope that it will be useful, but it *
! is provided "as is" and without any express or implied warranties.   *
! For more details see the full text of the license in the file        *
! LICENSE or in <http://www.gnu.org/licenses/>.                        *
!                                                                      *
! Copyright (C) 1996-2006, Thorstein Thorsteinsson                     *
!               1996-2006, David L. Cooper                             *
!***********************************************************************

subroutine projspn_cvb(bikcof,nel,nalf,nbet,ndet,ifns,minalf,maxalf,nkalf,minspn,maxspn,nkspn,locswp,lnoswp,locca,lnocca,ialfa, &
                       xdet,xspin,iw,detphase)

implicit real*8(a-h,o-w,y-z),integer(x)
logical done, first
dimension bikcof(ndet,ifns)
dimension minalf(0:nel), maxalf(0:nel), nkalf(0:nel)
dimension minspn(0:nel), maxspn(0:nel), nkspn(0:nel)
dimension locswp(2*nbet), lnoswp(2*nbet), locca(nel), lnocca(nel)
dimension ialfa(nalf), xdet(0:nel,0:nalf), xspin((nel+1)*(nalf+1))
dimension iw(nel), detphase(ndet)
integer rc

! Phase factors between alpha-beta separated determinants
! and determinants with ascending orbital numbers
call dcopy_(ndet,[1d0],0,detphase,1)
call asc2ab_cvb(detphase,1,nel,nalf)

! Projected spin functions (generated by orbital permutations on the first Kotani function)

! Set up maximum and minimum of spin functions:
do iorb=0,nel
  minspn(iorb) = iorb-min(iorb/2,nbet)
  maxspn(iorb) = min(iorb,nalf)
end do
call weight_cvb(xspin,minspn,maxspn,nalf,nel)

call imove_cvb(maxspn,nkspn,nel+1)
call occupy_cvb(nkspn,nel,iw,iw(nalf+1))
! Set up maximum and minimum for determinants:
do iorb=0,nel
  minalf(iorb) = max(iorb-nbet,0)
  maxalf(iorb) = min(iorb,nalf)
end do
call weight_cvb(xdet,minalf,maxalf,nalf,nel)
call imove_cvb(maxalf,nkalf,nel+1)
call occupy_cvb(nkalf,nel,locca,lnocca)
! Loop:
index = 1
first = .true.
do
  ! Skip first function
  if (first) then
    first = .false.
  else
    inddet = 1
    ! MAXSPN contains same elements as MAXALF
    call imove_cvb(maxspn,nkalf,nel+1)
    call occupy_cvb(nkalf,nel,locswp,lnoswp)
    do
      do i=1,nalf
        ialfa(i) = iw(locswp(i))
      end do
      do
        done = .false.
        do i=2,nalf
          if (ialfa(i) < ialfa(i-1)) then
            iswp = ialfa(i-1)
            ialfa(i-1) = ialfa(i)
            ialfa(i) = iswp
            done = .true.
            exit
          end if
        end do
        if (.not. done) exit
      end do
      bikcof(minind_cvb(ialfa,nalf,nel,xdet),index) = bikcof(inddet,1)*detphase(inddet)*detphase(minind_cvb(ialfa,nalf,nel,xdet))
      call loind_cvb(nel,nalf,nkalf,minalf,maxalf,locswp,lnoswp,inddet,xdet,rc)
      if (rc == 0) exit
    end do
  end if
  call loind_cvb(nel,nalf,nkspn,minspn,maxspn,iw,iw(nalf+1),index,xspin,rc)
  if (rc == 0) exit
end do

return

end subroutine projspn_cvb
