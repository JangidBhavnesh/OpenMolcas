!***********************************************************************
! This file is part of OpenMolcas.                                     *
!                                                                      *
! OpenMolcas is free software; you can redistribute it and/or modify   *
! it under the terms of the GNU Lesser General Public License, v. 2.1. *
! OpenMolcas is distributed in the hope that it will be useful, but it *
! is provided "as is" and without any express or implied warranties.   *
! For more details see the full text of the license in the file        *
! LICENSE or in <http://www.gnu.org/licenses/>.                        *
!                                                                      *
! Copyright (C) 1991, Roland Lindh                                     *
!               2003, Hans-Joachim Werner                              *
!***********************************************************************

subroutine Drv1_Pot(FD,CCoor,pot,ngrid,ncmp,nordop)
!***********************************************************************
!                                                                      *
! Object: to compute the potential on a grid using density FD (triang) *
!         Grid provided in CCoor(3,ngrid)                              *
!         Potential values returned in pot                             *
!                                                                      *
! Generated by modification of drv_rf.f, naint.f, sogthr.f             *
! H.-J. Werner, Jan 2003                                               *
!***********************************************************************

use Symmetry_Info, only: nIrrep
use Real_Spherical
use iSD_data
use Basis_Info
use Center_Info
use Sizes_of_Seward, only: S

implicit real*8(A-H,O-Z)
#include "angtp.fh"
#include "real.fh"
#include "WrkSpc.fh"
#include "print.fh"
#include "nsd.fh"
#include "setup.fh"
real*8 A(3), B(3), FD(*), CCoor(3,ngrid), RB(3), TRB(3), TA(3), pot(ncmp,ngrid)
logical AeqB
character ChOper(0:7)*3
integer iStabO(0:7), iDCRR(0:7), iDCRT(0:7), iStabM(0:7), nOp(3)
data ChOper/'E  ','x  ','y  ','xy ','z  ','xz ','yz ','xyz'/
! Statement functions
nElem(ixyz) = (ixyz+1)*(ixyz+2)/2

!                                                                      *
!***********************************************************************
!                                                                      *
iRout = 112
iPrint = nPrint(iRout)

loper = 2**nIrrep-1
nComp = (nOrdOp+1)*(nOrdOp+2)/2
if (ncomp /= ncmp) then
  call WarningMessage(2,'Drv1_pot: ncmp < ncomp')
  write(6,*) 'ncmp < ncomp:',ncmp,ncomp
  call Abend()
end if
call fzero(pot,ncmp*ngrid)

! Auxiliary memory allocation.

call GetMem('Zeta','ALLO','REAL',iZeta,S%m2Max)
call GetMem('Zeta','ALLO','REAL',ipZI,S%m2Max)
call GetMem('Kappa','ALLO','REAL',iKappa,S%m2Max)
call GetMem('PCoor','ALLO','REAL',iPCoor,S%m2Max*3)
!                                                                      *
!***********************************************************************
!                                                                      *
call Nr_Shells(nSkal)
!                                                                      *
!***********************************************************************
!                                                                      *
! Double loop over shells. These loops decide the integral type

do iS=1,nSkal
  iShll = iSD(0,iS)
  iAng = iSD(1,iS)
  iCmp = iSD(2,iS)
  iBas = iSD(3,iS)
  iPrim = iSD(5,iS)
  iAO = iSD(7,iS)
  mdci = iSD(10,iS)
  iShell = iSD(11,iS)
  iCnttp = iSD(13,iS)
  iCnt = iSD(14,iS)
  A(1:3) = dbsc(iCnttp)%Coor(1:3,iCnt)
  do jS=1,iS
    jShll = iSD(0,jS)
    jAng = iSD(1,jS)
    jCmp = iSD(2,jS)
    jBas = iSD(3,jS)
    jPrim = iSD(5,jS)
    jAO = iSD(7,jS)
    mdcj = iSD(10,jS)
    jShell = iSD(11,jS)
    jCnttp = iSD(13,jS)
    jCnt = iSD(14,jS)
    B(1:3) = dbsc(jCnttp)%Coor(1:3,jCnt)

    iSmLbl = 1
    nSO = MemSO1(iSmLbl,iCmp,jCmp,iShell,jShell,iAO,jAO)
    if (nSO == 0) Go To 131
    if (iPrint >= 19) write(6,'(A,A,A,A,A)') ' ***** (',AngTp(iAng),',',AngTp(jAng),') *****'

    ! Call kernel routine to get memory requirement.

    !call NAMem(nOrder,MemKer,iAng,jAng,nOrdOp)
    call EFMmP(nOrder,MemKer,iAng,jAng,nOrdOp)
    MemKrn = MemKer*S%m2Max
    call GetMem('Kernel','ALLO','REAL',iKern,MemKrn)

    ! Allocate memory for the final integrals, all in the primitive basis.

    lFinal = 1
    if (nOrdOp /= 0) then
      lFinal = S%MaxPrm(iAng)*S%MaxPrm(jAng)*nElem(iAng)*nElem(jAng)*nComp
    end if
    call GetMem('Final','ALLO','REAL',ipFnl,lFinal)

    ! Scratch area for contraction step

    nScr1 = S%MaxPrm(iAng)*S%MaxPrm(jAng)*nElem(iAng)*nElem(jAng)
    call GetMem('Scrtch','ALLO','REAL',iScrt1,nScr1)

    ! Scratch area for the transformation to spherical gaussians

    nScr2 = S%MaxPrm(iAng)*S%MaxPrm(jAng)*nElem(iAng)*nElem(jAng)
    call GetMem('ScrSph','Allo','Real',iScrt2,nScr2)

    nDAO = iPrim*jPrim*nElem(iAng)*nElem(jAng)
    call GetMem(' DAO ','Allo','Real',ipDAO,nDAO)

    ! At this point we can compute Zeta.

    call ZXia(Work(iZeta),Work(ipZI),iPrim,jPrim,Shells(iShll)%Exp,Shells(jShll)%Exp)

    AeqB = iS == jS

    ! Find the DCR for A and B

    call DCR(LmbdR,dc(mdci)%iStab,dc(mdci)%nStab,dc(mdcj)%iStab,dc(mdcj)%nStab,iDCRR,nDCRR)
    if (iPrint >= 49) write(6,'(10A)') ' {R}=(',(ChOper(iDCRR(i)),i=0,nDCRR-1),')'

    ! Find the stabilizer for A and B

    call Inter(dc(mdci)%iStab,dc(mdci)%nStab,dc(mdcj)%iStab,dc(mdcj)%nStab,iStabM,nStabM)

    ! Allocate memory for the elements of the Fock or 1st order
    ! density matrix which are associated with the current shell pair.

    call GetMem('DSOpr ','ALLO','REAL',ipDSOp,nSO*iPrim*jPrim)
    call GetMem('DSO ','ALLO','REAL',ipDSO,nSO*iPrim*jPrim)

    ! Gather the elements from 1st order density / Fock matrix.

    call SOGthr(Work(ipDSO),iBas,jBas,nSO,FD,n2Tri(iSmLbl),iSmLbl,iCmp,jCmp,iShell,jShell,AeqB,iAO,jAO)

    ! Project the Fock/1st order density matrix in AO
    ! basis on to the primitive basis.

    if (iPrint >= 99) then
      call RecPrt(' Left side contraction',' ',Shells(iShll)%pCff,iPrim,iBas)
      call RecPrt(' Right side contraction',' ',Shells(jShll)%pCff,jPrim,jBas)
    end if

    ! Transform IJ,AB to J,ABi
    call DGEMM_('T','T',jBas*nSO,iPrim,iBas,1.0d0,Work(ipDSO),iBas,Shells(iShll)%pCff,iPrim,0.0d0,Work(ipDSOp),jBas*nSO)
    ! Transform J,ABi to AB,ij
    call DGEMM_('T','T',nSO*iPrim,jPrim,jBas,1.0d0,Work(ipDSOp),jBas,Shells(jShll)%pCff,jPrim,0.0d0,Work(ipDSO),nSO*iPrim)
    ! Transpose to ij,AB
    call DGeTmO(Work(ipDSO),nSO,nSO,iPrim*jPrim,Work(ipDSOp),iPrim*jPrim)
    call GetMem('DSO ','Free','Real',ipDSO,nSO*iPrim*jPrim)

    if (iPrint >= 99) call RecPrt(' Decontracted 1st order density/Fock matrix',' ',Work(ipDSOp),iPrim*jPrim,nSO)

    ! Loops over symmetry operations.

    do lDCRR=0,nDCRR-1
      call OA(iDCRR(lDCRR),B,RB)

      ! Generate stabilizer of the operator.

      call SOS(iStabO,nStabO,lOper)

      ! Find the DCR for M and S

      call DCR(LmbdT,iStabM,nStabM,iStabO,nStabO,iDCRT,nDCRT)
      if (iPrint >= 49) then
        write(6,'(10A)') ' {M}=(',(ChOper(iStabM(i)),i=0,nStabM-1),')'
        write(6,'(10A)') ' {O}=(',(ChOper(iStabO(i)),i=0,nStabO-1),')'
        write(6,'(10A)') ' {T}=(',(ChOper(iDCRT(i)),i=0,nDCRT-1),')'
      end if

      ! Compute normalization factor due the DCR symmetrization
      ! of the two basis functions and the operator.

      iuv = dc(mdci)%nStab*dc(mdcj)%nStab
      FactNd = dble(iuv*nStabO)/dble(nIrrep**2*LmbdT)
      if (MolWgh == 1) then
        FactNd = FactNd*dble(nIrrep)**2/dble(iuv)
      else if (MolWgh == 2) then
        FactNd = sqrt(dble(iuv))*dble(nStabO)/dble(nIrrep*LmbdT)
      end if

      do lDCRT=0,nDCRT-1
        nOp(1) = NrOpr(iDCRT(lDCRT))
        nOp(2) = NrOpr(ieor(iDCRT(lDCRT),iDCRR(lDCRR)))
        nOp(3) = NrOpr(0)

        call OA(iDCRT(lDCRT),A,TA)
        call OA(iDCRT(lDCRT),RB,TRB)
        if (iPrint >= 49) then
          write(6,'(A,/,3(3F6.2,2X))') ' *** Centers A, B, C ***',(TA(i),i=1,3),(TRB(i),i=1,3)
          write(6,*) ' nOp=',nOp
        end if

        ! Desymmetrize the matrix with which we will contract the trace.

        call DesymD(iSmLbl,iAng,jAng,iCmp,jCmp,iShell,jShell,iShll,jShll,iAO,jAO,Work(ipDAO),iPrim,jPrim,Work(ipDSOp),nSO,nOp, &
                    FactNd)

        ! Project the spherical harmonic space onto the cartesian space.

        kk = nElem(iAng)*nElem(jAng)
        if (Shells(iShll)%Transf .or. Shells(jShll)%Transf) then

          ! ij,AB --> AB,ij
          call DGeTmO(Work(ipDAO),iPrim*jPrim,iPrim*jPrim,iCmp*jCmp,Work(iScrt1),iCmp*jCmp)
          ! AB,ij --> ij,ab
          call SphCar(Work(iScrt1),iCmp*jCmp,iPrim*jPrim,Work(iScrt2),nScr2,RSph(ipSph(iAng)),iAng,Shells(iShll)%Transf, &
                      Shells(iShll)%Prjct,RSph(ipSph(jAng)),jAng,Shells(jShll)%Transf,Shells(jShll)%Prjct,Work(ipDAO),kk)
        end if
        if (iPrint >= 99) call RecPrt(' Decontracted FD in the cartesian space',' ',Work(ipDAO),iPrim*jPrim,kk)

        ! Compute kappa and P.

        call Setup1(Shells(iShll)%Exp,iPrim,Shells(jShll)%Exp,jPrim,TA,TRB,Work(iKappa),Work(iPCoor),Work(ipZI))

        ! Compute primitive potential integrals and trace with density

        call potintd(Shells(iShll)%Exp,iPrim,Shells(jShll)%Exp,jPrim,Work(iZeta),Work(ipZI),Work(iKappa),Work(iPCoor),iPrim*jPrim, &
                     iAng,jAng,TA,TRB,nOrder,Work(iKern),MemKer,work(ipFnl),lFinal,nDAO,Ccoor,pot,ngrid,ncmp,Work(ipDAO),nOrdOp)

      end do
    end do

    call GetMem('DSOpr ','Free','REAL',ipDSOp,nSO*iPrim*jPrim)
    call GetMem(' DAO ','Free','Real',ipDAO,nDAO)
    call GetMem('ScrSph','Free','Real',iScrt2,nScr2)
    call GetMem('Scrtch','Free','Real',iScrt1,nScr1)
    call GetMem('Final','Free','Real',ipFnl,lFinal)
    call GetMem('Kernel','Free','Real',iKern,MemKrn)
131 continue
  end do
end do
if (nOrdOp == 2) then
  ! modifify field gradients to get traceless form
  ThreeI = One/Three
  do igeo=1,ngrid
    XX = Two*pot(1,igeo)-pot(4,igeo)-pot(6,igeo)
    YY = Two*pot(4,igeo)-pot(1,igeo)-pot(6,igeo)
    ZZ = Two*pot(6,igeo)-pot(4,igeo)-pot(1,igeo)
    pot(1,igeo) = XX*ThreeI
    pot(4,igeo) = YY*ThreeI
    pot(6,igeo) = ZZ*ThreeI
  end do
end if

call GetMem('PCoor','FREE','REAL',iPCoor,S%m2Max*3)
call GetMem('Kappa','FREE','REAL',iKappa,S%m2Max)
call GetMem('Zeta','FREE','REAL',ipZI,S%m2Max)
call GetMem('Zeta','FREE','REAL',iZeta,S%m2Max)

return

end subroutine Drv1_Pot
!====
subroutine PotIntd(Alpha,nAlpha,Beta,nBeta,Zeta,ZInv,rKappa,P,nZeta,la,lb,A,RB,nRys,Array,nArr,final,lFinal,nDAO,CCoor,pot,ngrid, &
                   ncmp,DAO,nOrdOp)
!***********************************************************************
!                                                                      *
! Object: kernel routine for the computation of potential (nordop=0)   *
!         or electric field (nordop > 0) on a grid                     *
!                                                                      *
!     Author: Roland Lindh, Dept. of Theoretical Chemistry, University *
!             of Lund, Sweden, January '91                             *
!***********************************************************************

use k2_arrays
implicit real*8(A-H,O-Z)
! Used for normal nuclear attraction integrals
external TNAI, Fake, XCff2D, XRys2D
#include "real.fh"
#include "WrkSpc.fh"
#include "oneswi.fh"
!#include "print.fh"
real*8 Zeta(nZeta), ZInv(nZeta), Alpha(nAlpha), Beta(nBeta), rKappa(nZeta), P(nZeta,3), A(3), RB(3), CCoor(3,*), &
       Array(nZeta*nArr), pot(ncmp,ngrid), DAO(*), final(lfinal)
! Local arrays
!real*8 C(3), TC(3), Coori(3,4), CoorAC(3,2)
real*8 TC(3), Coori(3,4), CoorAC(3,2)
logical EQ, NoSpecial
integer iAnga(4)
!character ChOper(0:7)*3
!data ChOper/'E  ','x  ','y  ','xy ','z  ','xz ','yz ','xyz'/
! Statement functions
nElem(ixyz) = (ixyz+1)*(ixyz+2)/2
nabSz(ixyz) = (ixyz+1)*(ixyz+2)*(ixyz+3)/6-1

nComp = (nOrdOp+1)*(nOrdOp+2)/2

iAnga(1) = la
iAnga(2) = lb
iAnga(3) = nOrdOp
iAnga(4) = 0
call dcopy_(3,A,1,Coori(1,1),1)
call dcopy_(3,RB,1,Coori(1,2),1)
mabMin = nabSz(max(la,lb)-1)+1
mabMax = nabSz(la+lb)
if (EQ(A,RB)) mabMin = nabSz(la+lb-1)+1
mcdMin = nabSz(nOrdOp-1)+1
mcdMax = nabSz(nOrdOp)
lab = (mabMax-mabMin+1)
kab = nElem(la)*nElem(lb)
lcd = (mcdMax-mcdMin+1)
labcd = lab*lcd

! Compute FLOP's and size of work array which Hrr will use.

call mHrr(la,lb,nFLOP,nMem)

! Distribute the work array

ip1 = 1+nZeta*max(labcd,lcd*nMem)
mArr = nArr-max(labcd,lcd*nMem)

! Find center to accumulate angular momentum on. (HRR)

if (la >= lb) then
  call dcopy_(3,A,1,CoorAC(1,1),1)
else
  call dcopy_(3,RB,1,CoorAC(1,1),1)
end if

nT = nZeta
NoSpecial = .true.

do igeo=1,ngrid
  do i=1,3
    TC(i) = CCoor(i,igeo)
    CoorAC(i,2) = TC(i)
    Coori(i,3) = TC(i)
    Coori(i,4) = TC(i)
  end do

  ! Compute integrals with the Rys quadrature.

  call Rys(iAnga,nT,Zeta,ZInv,nZeta,[One],[One],1,P,nZeta,TC,1,rKappa,[One],Coori,Coori,CoorAC,mabMin,mabMax,mcdMin,mcdMax, &
           Array(ip1),mArr*nZeta,TNAI,Fake,XCff2D,XRys2D,NoSpecial)

  if (nOrdOp == 0) then

    ! Use the HRR to compute the required primitive integrals.

    call HRR(la,lb,A,RB,Array(ip1),nZeta,nMem,ipIn)
    ipc = ip1-1+ipin

    ! Trace primitive integrals with density

    pot(1,igeo) = pot(1,igeo)-ddot_(nDAO,Array(ipc),1,DAO,1)
  else
    call DGetMO(Array(ip1),nZeta*lab,nZeta*lab,lcd,Array,lcd)
    call HRR(la,lb,A,RB,Array,lcd*nZeta,nMem,ipIn)
    call DGetMO(Array(ipIn),lcd,lcd,nZeta*kab,final,nZeta*kab)
    ipc = 1
    do icmp=1,ncomp
      pot(icmp,igeo) = pot(icmp,igeo)+ddot_(nDAO,final(ipc),1,DAO,1)
      ipc = ipc+nDAO
    end do
  end if
end do

return
! Avoid unused argument warnings
if (.false.) then
  call Unused_real_array(Alpha)
  call Unused_real_array(Beta)
  call Unused_integer(nRys)
end if

end subroutine PotIntd
