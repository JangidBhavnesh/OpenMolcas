!***********************************************************************
! This file is part of OpenMolcas.                                     *
!                                                                      *
! OpenMolcas is free software; you can redistribute it and/or modify   *
! it under the terms of the GNU Lesser General Public License, v. 2.1. *
! OpenMolcas is distributed in the hope that it will be useful, but it *
! is provided "as is" and without any express or implied warranties.   *
! For more details see the full text of the license in the file        *
! LICENSE or in <http://www.gnu.org/licenses/>.                        *
!                                                                      *
! Copyright (C) 1990, Markus P. Fuelscher                              *
!               1990, Jeppe Olsen                                      *
!***********************************************************************

!#define _DEBUGPRINT_
subroutine REORD(SGS,CIS,EXS,NCONF,IMODE,ICONF,ISPIN,kSym,CIOLD)
! AUTHOR:  M.P. FUELSCHER AND J. OLSEN
!          UNIV. OF LUND, SWEDEN 1990
!
! PURPOSE: CONSTRUCT THE REINDEXING ARRAY WHICH REORDERS
!          THE CSFS GENERATED BY THE DETERMINANT CODE INTO
!          THE SPLIT GRAPH GUGA ORDER.
!          FOR THAT CONSTRUCT FOR EACH CSF THE CORRESPONDING
!          STEP VECTOR AND PASS IT TO THE FUNCTIONS IPHASE
!          AND ISGNUM WHICH COMPUTES THE THE PHASE FACTOR
!          INVOLVED WHEN GOING FROM THE SYMMETRIC TO THE
!          UNITARY GROUP AND THE SPLIT ORDERING NUMBER.
!          FINALLY, THE VECTORS OF CI COEFFICIENTS ARE
!          REORDERED AND, TWO MODE ARE POSSIBLE:
!          IMODE=0 : FROM SYMMETRIC GROUP TO SPLIT GRAPH UGA ORDER
!          IMODE=1 : FROM SPLIT GRAPH UGA TO SYMMETRIC GROUP ORDER

#ifdef _DEBUGPRINT_
use Definitions, only: u6
#endif
use MCLR_data, only: minop, NCSFTP => NCPCNT, NCNFTP => NCNATS, NTYP
use stdalloc, only: mma_allocate, mma_deallocate
use gugx, only: SGStruct, CIStruct, EXStruct

implicit none
type(SGStruct) SGS
type(CIStruct) CIS
type(EXStruct) EXS
integer kSym, nCONF, IMODE
integer ICONF(*), ISPIN(*)
real*8 CIOLD(NCONF)
real*8, allocatable :: CINEW(:)
integer IWALK(50)
integer, external :: IPHASE, ISGNUM
integer :: IC, ICL, ICNBS, ICNBS0, ICSBAS, ICSFJP, IICSF, IOPEN, IP, IPBAS, ISG, ITYP
real*8 PHASE
#ifdef _DEBUGPRINT_
integer I
#endif

call mma_allocate(CINEW,NCONF,Label='CINEW')

associate(nLev => SGS%nLev,IDRT => SGS%DRT,IDOWN => SGS%Down,IDAW => SGS%DAW,IUP => SGS%Up,IRAW => SGS%RAW,IUSGN => EXS%USGN, &
          ILSGN => EXS%LSGN,nVert => SGS%nVert,MidLev => SGS%MidLev,MVSta => SGS%MVSta,nMidV => CIS%nMidV,MXUP => SGS%MxUp, &
          MXDWN => SGS%MxDwn,nEl => SGS%nActEl,NORB => SGS%nLev)

  ! LOOP OVER CONFIGURATIONS TYPES

  ICSFJP = 0
  ICNBS0 = 0 ! dummy initialize
  IPBAS = 0 ! dummy initialize
  do ITYP=1,NTYP
    IOPEN = ITYP+MINOP-1 ! MINOP IS NOT INITIALIZED TEOEAW
    ICL = (NEL-IOPEN)/2
    ! BASE ADDRESS FOR CONFIGURATION OF THIS TYPE
    if (ITYP == 1) then
      ICNBS0 = 1
    else
      ICNBS0 = ICNBS0+NCNFTP(ITYP-1,kSym)*(NEL+IOPEN-1)/2
    end if
    ! BASE ADDRESS FOR PROTOTYPE SPIN COUPLINGS
    if (ITYP == 1) then
      IPBAS = 1
    else
      IPBAS = IPBAS+NCSFTP(ITYP-1)*(IOPEN-1)
    end if

    ! LOOP OVER NUMBER OF CONFIGURATIONS OF TYPE ITYP AND PROTOTYPE
    ! SPIN COUPLINGS

    do IC=1,NCNFTP(ITYP,kSym)
      ICNBS = ICNBS0+(IC-1)*(IOPEN+ICL)
      do IICSF=1,NCSFTP(ITYP)
        ICSFJP = ICSFJP+1
        ICSBAS = IPBAS+(IICSF-1)*IOPEN
        ! COMPUTE STEP VECTOR
        call STEPVEC(ICONF(ICNBS),ICONF(ICNBS+ICL),ICL,IOPEN,ISPIN(ICSBAS),NORB,IWALK)
        ! GET SPLIT GRAPH ORDERING NUMBER
        ! FUNCTION ISGNUM
        ISG = ISGNUM(NLEV,NVERT,MIDLEV,MVSta,NMIDV,MXUP,MXDWN,IDOWN,IUP,IDAW,IRAW,IUSGN,ILSGN,IWALK)
        ! GET PHASE PHASE FACTOR
        IP = IPHASE(NLEV,NVERT,IDRT,IUP,IWALK)
        ! NOW REORDER THIS ELEMENT OF THE CI-VECTOR
        PHASE = dble(IP)
        select case (iMode)
          case (0)
            CINEW(ISG) = CIOLD(ICSFJP)*PHASE
          case (1)
            CINEW(ICSFJP) = CIOLD(ISG)*PHASE
        end select
      end do
    end do
  end do

# ifdef _DEBUGPRINT_
  write(u6,*)
  write(u6,*) ' OLD CI-VECTORS IN SUBROUTINE REORD'
  write(u6,'(10F12.8)') (CIOLD(I),I=1,NCONF)
  write(u6,*) ' NEW CI-VECTORS IN SUBROUTINE REORD'
  write(u6,'(10F12.8)') (CINEW(I),I=1,NCONF)
  write(u6,*)
# endif

end associate
CIOLD(:) = CINEW(:)
call mma_deallocate(CINEW)

end subroutine REORD
