!***********************************************************************
! This file is part of OpenMolcas.                                     *
!                                                                      *
! OpenMolcas is free software; you can redistribute it and/or modify   *
! it under the terms of the GNU Lesser General Public License, v. 2.1. *
! OpenMolcas is distributed in the hope that it will be useful, but it *
! is provided "as is" and without any express or implied warranties.   *
! For more details see the full text of the license in the file        *
! LICENSE or in <http://www.gnu.org/licenses/>.                        *
!***********************************************************************

!----------------------------------------------------------------------*
! This subroutine reads from the formatted file DIFFPR coming from     *
! generated by MpProp the Slater Exponents, Factors and Nuclear Charges*
!----------------------------------------------------------------------*
subroutine Get_Slater(SlExpQ,LMltSlQ,outxyz,nAt)

use Definitions, only: wp, iwp, u6

implicit none
#include "maxi.fh"
#include "warnings.h"
real(kind=wp) :: SlExpQ(MxMltp+1,MxQCen), outxyz(MxQCen,3)
integer(kind=iwp) :: iC, ind, jhr, k, kk, l, LMltSlQ, Lu, nAt, nS, nSlCentQ, nT, nTestjhr
real(kind=wp) :: CoordTest(3), SlFactQ(6)
logical(kind=iwp) :: Exist, lCheck
integer(kind=iwp), external :: IsFreeUnit

! Open the file
Lu = IsFreeUnit(40)
call Opnfl('DIFFPR',Lu,Exist)
if (.not. Exist) then
  write(u6,*)
  write(u6,*) ' Can not locate output file DiffPr. '
  call Quit(_RC_IO_ERROR_READ_)
end if
rewind(Lu)

! Read Number of centers and angular momentums.

read(Lu,101) nSlCentQ
read(Lu,101) LMltSlQ

! A first test
nTestjhr = nAt*(nAt+1)/2
if (nSlCentQ /= nTestjhr) then
  write(u6,*) 'ERROR! Number of centers in DiffPr file',nSlCentQ,' is different from number of centers obtained from RUNFILE', &
              nTestjhr,' Check your files.'
  call Quit(_RC_GENERAL_ERROR_)
end if

! Read Exponentials for the Centers
do iC=1,nSlCentQ
  lCheck = .false.
  read(Lu,103) (CoordTest(k),k=1,3)
  ind = 0
  do jhr=1,nSlCentQ
    if (abs(CoordTest(1)-outxyz(jhr,1)) < 1.0e-4_wp) then
      if (abs(CoordTest(2)-outxyz(jhr,2)) < 1.0e-4_wp) then
        if (abs(CoordTest(3)-outxyz(jhr,3)) < 1.0e-4_wp) then
          lCheck = .true.
          ind = jhr
        end if
      end if
    end if
  end do
  if (.not. lCheck) then
    write(u6,*) 'ERROR. Something is very wrong, coordinates of DiffPr and MpProp files do not match. DiffPr center',iC
  end if
  do l=0,LMltSlQ
    nS = l*(l+1)*(l+2)/6
    nT = (l+1)*(l+2)*(l+3)/6
    read(Lu,104) SlExpQ(l+1,ind)
    read(Lu,105) (SlFactQ(kk),kk=nS+1,nT)
    !read(Lu,105) (SlFactQ(kk,ind),kk=nS+1,nT)
  end do
  !Jose. No read nuclear charge
  !read(Lu,104) PointP(ind)
  read(Lu,*)
end do

close(Lu)

return
#ifdef _WARNING_WORKAROUND_
if (.false.) call Unused_real_array(SlFactQ)
#endif

101 format(I5)
103 format(3(F20.14))
104 format(F20.14)
105 format(3(F20.14))

end subroutine Get_Slater
